# $Id: configure.in,v 1.13 2004/02/26 17:43:47 grubba Exp $
AC_INIT(nettle.cmod)
AC_CONFIG_HEADER(nettle_config.h)

AC_SET_MAKE

AC_MODULE_INIT()

AC_CHECK_LIB(gmp, mpz_init)
AC_CHECK_LIB(gmp, __mpz_init)
AC_CHECK_LIB(gmp, __gmpz_init)
AC_CHECK_LIB(nettle, nettle_md5_init)
AC_CHECK_LIB(nettle, md5_init)

if test "x$ac_cv_lib_nettle_nettle_md5_init" = "xyes" -o \
        "x$ac_cv_lib_nettle_md5_init" = "xyes"; then
   PIKE_FEATURE_OK(Nettle)

   # md4 is available in later versions of nettle
   AC_CHECK_FUNCS(nettle_md4_init)

   AC_MSG_CHECKING([for idea.c])
   # Have the patent-encumbered idea files been purged?
   if test -f "$srcdir/idea.c" ; then
      AC_MSG_RESULT([found])
      AC_DEFINE([WITH_IDEA], 1, [idea.c available])
   else
      AC_MSG_RESULT([no])	
   fi
else
   if test "$ac_cv_lib_gmp_mpz_init:$ac_cv_lib_gmp___mpz_init:$ac_cv_lib_gmp___gmpz_init" = "no:no:no"; then
      PIKE_ENABLE_BUNDLE(gmp, [
         ac_cv_lib_gmp_mpz_init
         ac_cv_lib_gmp___mpz_init
         ac_cv_lib_gmp___gmpz_init
      ])
   fi
   PIKE_ENABLE_BUNDLE(nettle, [
      ac_cv_lib_nettle_nettle_md5_init
      ac_cv_lib_nettle_md5_init
   ])
   AC_MSG_WARN([The nettle library not found.
No crypto functions will be available.]) 
   PIKE_FEATURE_NODEP(Nettle)
fi

AC_OUTPUT(Makefile,echo FOO >stamp-h )
