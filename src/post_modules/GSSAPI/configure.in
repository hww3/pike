# $Id: configure.in,v 1.2 2008/07/22 19:33:05 mast Exp $

AC_INIT(gssapi.cmod)
AC_CONFIG_HEADER(gssapi_config.h)
AC_ARG_WITH(gssapi, MY_DESCR([--without-gssapi], [Disable support for GSS-API]),
	    [], [with_gssapi=yes])

AC_MODULE_INIT()
PIKE_FEATURE_WITHOUT(GSSAPI)

if test x$with_gssapi = xyes; then

  PIKE_FEATURE(GSSAPI, [no (gssapi.h not found)])
  AC_CHECK_HEADERS(gssapi.h gssapi/gssapi.h, break)
  AC_CHECK_HEADERS(gssapi_krb5.h gssapi/gssapi_krb5.h, break)

  if test x$ac_cv_header_gssapi_h = xyes -o \
	  x$ac_cv_header_gssapi_gssapi_h = xyes; then
    PIKE_FEATURE(GSSAPI, [no (GSS-API v2 library not found)])

    # Check for gss_wrap since it only exists in GSS-API version 2.
    # Must use PIKE_SEARCH_LIBS since the lib in the Windows dist
    # (KfW) uses __stdcall.
    PIKE_SEARCH_LIBS(gss_wrap, [
      gss_wrap (NULL, GSS_C_NO_CONTEXT, 0, GSS_C_QOP_DEFAULT,
		GSS_C_NO_BUFFER, NULL, GSS_C_NO_BUFFER);
    ], gssapi gssapiv2 gssapi_krb5 gssapi32, [
      AC_DEFINE(HAVE_GSSAPI)
      PIKE_FEATURE(GSSAPI, [yes ($pike_cv_search_gss_wrap)])
    ])

    AC_CHECK_FUNCS(gss_inquire_mechs_for_name)
  fi
fi

AC_OUTPUT(Makefile)
