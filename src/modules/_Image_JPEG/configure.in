#
# $Id: configure.in,v 1.16 2002/10/07 11:55:39 kiwi Exp $
#
AC_INIT(image_jpeg.c)
AC_CONFIG_HEADER(config.h)
AC_ARG_WITH(jpeglib,     [  --with(out)-jpeglib       Support JPEG (Image.JPEG)],[],[with_jpeglib=yes])

AC_MODULE_INIT()

PIKE_FEATURE_WITHOUT(Image.JPEG)

if test x$with_jpeglib = xyes ; then
  PIKE_FEATURE_NODEP(Image.JPEG)
  AC_CHECK_HEADERS(jpeglib.h)
  if test $ac_cv_header_jpeglib_h = yes ; then
    AC_CHECK_LIB(jpeg, jpeg_CreateCompress, [
      AC_DEFINE(HAVE_LIBJPEG) 
      LIBS="${LIBS-} -ljpeg"
      PIKE_FEATURE_OK(Image.JPEG)

      AC_MSG_CHECKING(for JERR_BAD_CROP_SPEC)
      AC_CACHE_VAL(pike_cv_jpeg_jerr_bad_crop_spec, [
        AC_TRY_COMPILE([
#include <jerror.h>
int main(int argc, char **argv)
{
  int err = JERR_BAD_CROP_SPEC;
  exit(0);
}
        ], [])
      ])
      if test "x$pike_cv_jpeg_jerr_bad_crop_spec" = "xyes"; then
        AC_MSG_RESULT(yes)
	AC_DEFINE(HAVE_JERR_BAD_CROP_SPEC)
      else
        AC_MSG_RESULT(no)
      fi
      AC_CHECK_FUNCS(jcopy_sample_rows jcopy_block_row jzero_far)
    ])
    AC_MSG_CHECKING([if jpeg_componment_info has the member height_in_blocks])
    AC_CACHE_VAL(pike_cv_jpeg_height_in_blocks, [
      AC_TRY_COMPILE([
#define FILE void
#define size_t unsigned int
#include <jpeglib.h>
#include <jconfig.h>
      ],[
  JDIMENSION block_test;
  jpeg_component_info *compptr;

  block_test = compptr->height_in_blocks;
      ],[
        pike_cv_jpeg_height_in_blocks=yes
      ],[
        pike_cv_jpeg_height_in_blocks=no
      ])
    ])
    AC_MSG_RESULT($pike_cv_jpeg_height_in_blocks)
    if test "x$pike_cv_jpeg_height_in_blocks" = "xyes"; then
      AC_DEFINE(HAVE_JPEG_HEIGHT_IN_BLOCKS)
    else :; fi
  fi
fi

AC_OUTPUT(Makefile,echo FOO >stamp-h )


