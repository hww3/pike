#
# $Id: configure.in,v 1.3 1997/01/05 21:44:13 grubba Exp $
#
# Configure script for the mysql-module
#
# Henrik Grubbström
#

#
# NOTE: 
#   Prior to 3.20.0          		After 3.20.0
#   --------------------------------------------------------------
#   /usr/local/mysql/mach-lib-threads	/usr/local/lib/mysql
#   /usr/local/mysql/include		/usr/local/include/mysql
#   libmysql.a				libmysqllib.a
#   libstrings.a			libmystrings.a
#
#   After 3.20.0 pthreads are no longer needed.
#

AC_INIT(mysql.c)

AC_PROG_CC
AC_PROG_RANLIB
AC_SUBST(RANLIB)

AC_ARG_WITH(mysql,  [  --without-mysql       no support for the Mysql database],[],[with_mysql=yes])

if test x$with_mysql = xyes; then
  OLD_LDFLAGS=$LDFLAGS
  OLD_CPPFLAGS=$CPPFLAGS
  OLD_LIBS=$LIBS

  AC_MSG_CHECKING(Checking for Mysql lib-directory)

  AC_CACHE_VAL(pike_cv_mysql_lib_dir, [
    for pike_cv_mysql_lib_dir in /usr/local/lib/mysql /usr/gnu/lib/mysql /usr/lib/mysql /lib/mysql /usr/local/mysql/mach-lib-thread no; do
      if test -d $pike_cv_mysql_lib_dir/.; then
        break
      fi
    done
  ])

  AC_MSG_RESULT($pike_cv_mysql_lib_dir)

  if test x$pike_cv_mysql_lib_dir = xno; then :; else
    echo Adding $pike_cv_mysql_lib_dir to the library search path.
    LDFLAGS="-L$pike_cv_mysql_lib_dir ${LDFLAGS}"
  fi

  AC_MSG_CHECKING(Checking for Mysql include-directory)

  AC_CACHE_VAL(pike_cv_mysql_include_dir, [
    for pike_cv_mysql_include_dir in /usr/local/include/mysql /usr/gnu/include/mysql /usr/include/mysql /include/mysql /usr/local/mysql/include no; do
      if test -d $dir/.; then
        break
      fi
    done
  ])

  AC_MSG_RESULT($pike_cv_mysql_include_dir)

  if test x$pike_cv_mysql_include_dir = xno; then :; else
    echo Adding $pike_cv_mysql_include_dir to the include search path.
    CPPFLAGS="-I$pike_cv_mysql_include_dir ${CPPFLAGS}"
  fi

  # Mysql libs

  pike_cv_mysql="no"

  AC_CHECK_LIB(mystrings, bchange, [
    LIBS="-lmystrings $LIBS"
    pike_cv_mysql="post3.20"
  ], [
    AC_CHECK_LIB(strings, bchange, [
      LIBS="-lstrings $LIBS"
      pike_cv_mysql="pre3.20"
    ], [])
  ])

  AC_MSG_CHECKING(Mysql version)

  AC_MSG_RESULT($pike_cv_mysql)

  if test x$pike_cv_mysql = xno; then
    # Restore variables, so we don't link with unnessesary libs

    LIBS=$OLD_LIBS
    CPPFLAGS=$OLD_CPPFLAGS
    LDFLAGS=$OLD_LDFLAGS
  else

    # System libs which might be needed

    AC_CHECK_LIB(socket, socket, [], [])
    AC_CHECK_LIB(nsl, gethostbyname, [], [])
    AC_CHECK_LIB(m, floor, [], [])

    # Pthreads is still needed in 3.20.0.
    AC_CHECK_LIB(pthread, pthread_self, [], [])
    AC_CHECK_LIB(pthreads, pthread_self, [], [])

    AC_CHECK_LIB(dbug, _db_doprnt_, [], [])

    AC_CHECK_LIB(mysys, my_init, [], [])

    if test x$pike_cv_mysql = xpost3.20; then
      AC_CHECK_LIB(mysqllib, mysql_connect, [], [ pike_cv_mysql="no" ])
    else
      AC_CHECK_LIB(mysql, mysql_connect, [], [ pike_cv_mysql="no" ])
    fi

    if test x$pike_cv_mysql = xno; then
      # Restore variables, so we don't link with unnessesary libs

      LIBS=$OLD_LIBS
      CPPFLAGS=$OLD_CPPFLAGS
      LDFLAGS=$OLD_LDFLAGS
    else
      AC_DEFINE(HAVE_MYSQL)
    fi
  fi
fi

AC_OUTPUT(Makefile,echo FOO >stamp-h )
