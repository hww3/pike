#
# $Id: configure.in,v 1.9 2003/07/25 10:12:53 grubba Exp $
#
AC_INIT(image_tiff.c)
AC_CONFIG_HEADER(config.h)
AC_ARG_WITH(tifflib,     [  --with(out)-tifflib       Support TIFF (Image.TIFF)],[],[with_tifflib=yes])

AC_MODULE_INIT()

PIKE_FEATURE_WITHOUT(Image.TIFF)

if test x$with_tifflib = xyes ; then
  PIKE_FEATURE_NODEP(Image.TIFF)

  AC_CHECK_HEADERS(tiff.h tiffvers.h tiffio.h tiffiop.h)
  if test $ac_cv_header_tiff_h = yes ; then
    AC_MSG_CHECKING([if this tifflib is working])
    AC_CACHE_VAL(pike_cv_working_tiff, [
      # NOTE Disabled check!
      AC_EGREP_CPP(42, [
#include <tiff.h>
TIFF_VERSION
      ], [ pike_cv_working_tiff=yes ], [ pike_cv_working_tiff=yes ])
    ])
    if test "x$pike_cv_working_tiff" = "xno"; then
      AC_MSG_RESULT([no])
      PIKE_FEATURE([Image.TIFF],[no (tifflib version 42)])
    else
      AC_MSG_RESULT([yes])
      AC_CHECK_LIB(m, floor)
      AC_CHECK_LIB(tiff, main, [
  	AC_DEFINE(HAVE_LIBTIFF) 
  	LIBS="${LIBS-} -ltiff"
  	PIKE_FEATURE_OK(Image.TIFF)
       ],[])
      AC_CHECK_LIB(jpeg, main, [
  	LIBS="${LIBS-} -ljpeg"
       ],[])
      AC_CHECK_LIB(z, main, [
  	LIBS="${LIBS-} -lz"
       ],[])
      AC_MSG_CHECKING(for version of tifflib)
      if test "x$ac_cv_header_tiffvers_h" = "xyes"; then
        cat >conftest.$ac_ext <<EOF
#include <tiffvers.h>
VERSION:TIFFLIB_VERSION
EOF
        vers=`eval "$ac_cpp conftest.$ac_ext" 2>&5 | sed -e '/VERSION:/s/VERSION://p' -ed`
	if test "x$vers" = "x"; then
          AC_MSG_RESULT([Unknown (failed to parse tiffvers.h)])
	else
	  AC_MSG_RESULT([$vers])
        fi
	rm -f conftest*
      else
        AC_MSG_RESULT([Unknown (no tiffvers.h)])
      fi
    fi
  fi
fi

AC_OUTPUT(Makefile,echo FOO >stamp-h )


