#
# $Id: configure.in,v 1.8 2001/02/13 13:36:47 mirar Exp $
#
AC_INIT(image_ttf.c)
AC_CONFIG_HEADER(config.h)
AC_ARG_WITH(ttflib,     [  --with(out)-ttflib       Support TTF via ttf.lib (freetype) (Image.TTF)],[],[with_ttflib=yes])

AC_MODULE_INIT()

PIKE_FEATURE_WITHOUT(_Image_TTF,Image.TTF..........)

if test x$with_ttflib = xyes ; then
  PIKE_FEATURE_NODEP(_Image_TTF,Image.TTF..........)

  AC_CHECK_HEADERS(freetype.h ftxkern.h freetype/freetype.h freetype/ftxkern.h)

  parent="$ac_cv_header_ftxkern_h$ac_cv_header_freetype_h"
  subdir="$ac_cv_header_freetype_ftxkern_h$ac_cv_header_freetype_freetype_h"
  freetype_h="$ac_cv_header_freetype_h$ac_cv_header_freetype_freetype_h"

  if test "$parent" = "yesyes" -o "$subdir" = "yesyes"; then
    AC_CHECK_LIB(ttf, TT_Open_Collection, [
      AC_DEFINE(HAVE_LIBTTF) 
      LIBS="${LIBS-} -lttf"
      PIKE_FEATURE_OK(_Image_TTF,Image.TTF..........)
    ])
    AC_MSG_CHECKING([if TT_Horizontal_Header has the member Reserved0])
    AC_CACHE_VAL(pike_cv_tt_hh_reserved0, [
      AC_TRY_COMPILE([
#ifdef HAVE_FREETYPE_FREETYPE_H
#include <freetype/freetype.h>
#else
#include <freetype.h>
#endif
      ],[
  extern TT_Horizontal_Header *horiz;

  horiz->Reserved0 = 0;
      ],[
        pike_cv_tt_hh_reserved0=yes
      ], [
	pike_cv_tt_hh_reserved0=no
      ])
    ])
    AC_MSG_RESULT($pike_cv_tt_hh_reserved0)
    if test "x$pike_cv_tt_hh_reserved0" = "xyes"; then
      AC_DEFINE(HAVE_TT_H_H_Reserved0)
    else :; fi
  else
    if test "$freetype_h" = "nono"; then :; else
      PIKE_FEATURE(_Image_TTF,Image.TTF..........no (too old))
      AC_MSG_WARN(Detected freetype library (libttf), but too old version)
    fi
  fi
fi

AC_OUTPUT(Makefile,echo FOO >stamp-h )


