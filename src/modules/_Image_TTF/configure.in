#
# $Id: configure.in,v 1.6 1999/07/30 16:15:11 marcus Exp $
#
AC_INIT(image_ttf.c)
AC_CONFIG_HEADER(config.h)
AC_ARG_WITH(ttflib,     [  --with(out)-ttflib       Support TTF via ttf.lib (freetype) (Image.TTF)],[],[with_ttflib=yes])

AC_MODULE_INIT()

if test x$with_ttflib = xyes ; then
  AC_CHECK_HEADERS(freetype.h)
  AC_CHECK_HEADERS(ftxkern.h)
  if test $ac_cv_header_freetype_h = yes -a \
          $ac_cv_header_ftxkern_h = yes ; then
    AC_CHECK_LIB(ttf, TT_Open_Collection, [
      AC_DEFINE(HAVE_LIBTTF) 
      LIBS="${LIBS-} -lttf"
    ])
    AC_MSG_CHECKING([if TT_Horizontal_Header has the member Reserved0])
    AC_CACHE_VAL(pike_cv_tt_hh_reserved0, [
      AC_TRY_COMPILE([
#include <freetype.h>
      ],[
  extern TT_Horizontal_Header *horiz;

  horiz->Reserved0 = 0;
      ],[
        pike_cv_tt_hh_reserved0=yes
      ], [
	pike_cv_tt_hh_reserved0=no
      ])
    ])
    AC_MSG_RESULT($pike_cv_tt_hh_reserved0)
    if test "x$pike_cv_tt_hh_reserved0" = "xyes"; then
      AC_DEFINE(HAVE_TT_H_H_Reserved0)
    else :; fi
  elif test $ac_cv_header_freetype_h = yes -a \
          $ac_cv_header_ftxkern_h = no ; then
     AC_MSG_WARN(Detected freetype library (libttf), but too old version)
  fi
fi

AC_OUTPUT(Makefile,echo FOO >stamp-h )


